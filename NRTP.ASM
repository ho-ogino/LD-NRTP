

NRTDRV_LOADADRS		EQU	$1800			; NRTDRVの読み込み開始アドレス
NRTDRV_DRVINI		EQU	NRTDRV_LOADADRS+$00	; DRVINI
NRTDRV_MPLAY		EQU	NRTDRV_LOADADRS+$03	; MPLAY
NRTDRV_MSTOP		EQU	NRTDRV_LOADADRS+$06	; MSTOP
NRTDRV_MFADE		EQU	NRTDRV_LOADADRS+$09	; MFADE
NRTDRV_CTCRST		EQU	NRTDRV_LOADADRS+$0c
;NRTDRV_DRVINI2		EQU	NRTDRV_LOADADRS+$0c	; DRVINI
NRTDRV_MTEST		EQU	NRTDRV_LOADADRS+$0f	; MTEST
NRTDRV_MPLAYD		EQU	NRTDRV_LOADADRS+$12	; MPLAYD
NRTDRV_VINIT		EQU	NRTDRV_LOADADRS+$15	; 
NRTDRV_VMAIN		EQU	NRTDRV_LOADADRS+$18	; 
NRTDRV_INVREG		EQU	NRTDRV_LOADADRS+$1b	; 
NRTDRV_CTC3IO		EQU	NRTDRV_LOADADRS+$1e	; CTC3IO

NRTDRV_CTCFLAG		EQU	NRTDRV_LOADADRS+$20	; CTCフラグ
NRTDRV_PLAYFLAG		EQU	NRTDRV_LOADADRS+$24	; 演奏フラグ
NRTDRV_MVOL		EQU	NRTDRV_LOADADRS+$25	; MASTER VOLUME
NRTDRV_FADECOUNT	EQU	NRTDRV_LOADADRS+$26	; FCOUNT
NRTDRV_FADESPEED	EQU	NRTDRV_LOADADRS+$27	; FSPEED

NRTDRV_CTC3VAL		EQU	NRTDRV_LOADADRS+$2b	;
NRTDRV_CTC3SUB		EQU	NRTDRV_LOADADRS+$2c	;
NRTDRV_CTC0VAL		EQU	NRTDRV_LOADADRS+$2d	;
NRTDRV_CTC0SUB		EQU	NRTDRV_LOADADRS+$2e	;
NRTDRV_VSTAT		EQU	NRTDRV_LOADADRS+$2f
NRTDRV_VERSION		EQU	NRTDRV_LOADADRS+$31	; NRTDRV内部バージョン
NRTDRV_TRACKWORK	EQU	NRTDRV_LOADADRS+$32	; トラックワーク先頭アドレス
TRACKWORK_SIZE		EQU	56			; トラックワークのサイズ

BDOS			EQU	$0005
FSEARCH			EQU	$11
SETDTA			EQU	$1A
FOPEN			EQU	$0f
GETFSZ			EQU	$23
PRINT			EQU	$09

_CTC			EQU	$F08C			; LSX-Dodgers 1.52

; LSX-Dodgersの割り込みベクタ群
CTC0			EQU	$F0C0
CTC1			EQU	$F0C2
CTC2			EQU	$F0C4
CTC3			EQU	$F0C6
INTVEC			EQU	$F0C8

NRDADR		EQU	04000H				;曲データのデフォルトアドレス

	ORG	0100H

START:
	; 第一引数のファイルを読み込む
	LD	DE,$005C
	LD	C,FOPEN
	CALL	BDOS
	CP	$FF
	JR	NZ,OPENOK

	; 拡張子をNRDにして再度開いてみる
	LD	HL,DEFEXT
	LD	DE,$5C+9	; EXT
	LD	BC,3
	LDIR

	LD	DE,$005C
	LD	C,FOPEN
	CALL	BDOS
	CP	$FF
	; 開けない場合はエラーを出して終了
	JP	Z,FERREND
OPENOK:
	LD	DE,$005C
	LD	C,GETFSZ
	CALL	BDOS

	; DTAを設定する
	LD	C,SETDTA
	LD	DE,NRDADR
	CALL	BDOS

	; レコードサイズぶん読み込む
	LD	IX,$005C

	LD	(IX+14),1	;レコードサイズを1にする
	LD	(IX+15),0

	LD	(IX+33),0	;ランダムレコードを0にする
	LD	(IX+34),0
	LD	(IX+35),0
	LD	(IX+36),0

	LD	HL,($005C+16)	; RECORD SIZE?
	LD	DE,$005C
	LD	C,$27		; Random Block Read
	CALL	BDOS

	LD	DE,$005C
	LD	C,$10		; FCLOSE
	CALL	BDOS

	DI

	; CTCベクタを保存しておく
	LD	DE,CTCBK
	LD	HL,CTC0
	LD	BC,6
	LDIR

	; 簡易演奏モニタで演奏開始(ESCを押すと戻ってくる)
	CALL	DISPNRT

	; 演奏停止
	CALL	NRTDRV_MSTOP

	DI

	; 画面クリア
	LD	DE,TXCLR
	LD	C,PRINT
	CALL	BDOS

	; CTC復帰
	LD	DE,CTC0
	LD	HL,CTCBK
	LD	BC,6
	LDIR

	EI
	JP	0

FERREND:
	LD	C,0
	JP	DISPERR

DISPERR:
	LD	B,0
	SLA	C	; c = c * 2
	LD	HL,ERRTBL
	ADD	HL,BC
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	C,PRINT
	CALL	BDOS
	SCF
	JP	0

ERRTBL:
	DW	ERRMSG1
ERRMSG1:
	db	7,'File not found$'

TXCLR:
	db	01BH,'j$'

DEFEXT:
	DB	"NRD"

; 割り込みベクタ保存用
CTCBK:
	DW	0
	DW	0
	DW	0

	INCLUDE	"IPLPATCH.ASM"

	INCLUDE	"NRTDRV.ASM"

